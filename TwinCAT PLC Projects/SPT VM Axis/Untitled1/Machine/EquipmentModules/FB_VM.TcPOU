<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_VM" Id="{e8eb8946-ac6b-4f9a-a349-a2038fff9f99}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
FUNCTION_BLOCK FB_VM EXTENDS FB_PackML_BaseModule
VAR
	Axis : FB_Component_BasicSlaveAxis := (Name := 'Virtual Master Axis');
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods" Id="{ab06e52a-d0cf-4098-b8b2-384220a5d636}" />
    <Folder Name="Properties" Id="{79f964a1-6ef0-45b9-9994-3fc01e580b1f}" />
    <Method Name="Aborting" Id="{0ab2a755-f479-4155-bea8-7b1b108018b5}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD PROTECTED Aborting]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Mode is ignored
CASE SequenceState OF
	0:
		NoStateTasksToComplete := FALSE;
		SequenceState		   := SequenceState + 10;

	10: // Stop Axis
		Axis.Override := 100;
		Axis.Stop(); // This will also GearOut
		IF Axis.Stopped THEN
			SequenceState := SequenceState + 10;
		END_IF

	20: // Disable Axis
		Axis.Disable();
		IF NOT Axis.Enabled THEN
			StateTasksComplete := TRUE;
		END_IF
END_CASE

SUPER^.Aborting();]]></ST>
      </Implementation>
    </Method>
    <Property Name="AxisRef" Id="{8fbb3c08-11e6-42e9-be14-c11d172359bb}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY AxisRef : REFERENCE TO AXIS_REF]]></Declaration>
      <Get Name="Get" Id="{2a15330a-5ee0-4def-aebb-a9f06cee8655}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[AxisRef REF= Axis.Axis;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Completing" Id="{3ff898da-49a5-42a9-84dc-dd8eaed69c93}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD PROTECTED Completing]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE _CurrentMode OF
	ePMLUnitMode_Production:
		CASE SequenceState OF
			0:
				NoStateTasksToComplete := FALSE;
				SequenceState		   := SequenceState + 10;

			10: // Stop Axis
				Axis.Stop();
				IF Axis.Stopped THEN
					StateTasksComplete := TRUE;
				END_IF
		END_CASE
END_CASE

SUPER^.Completing();]]></ST>
      </Implementation>
    </Method>
    <Method Name="CyclicLogic" Id="{099d02ae-52c4-4630-a76e-d553625d2bca}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD PUBLIC CyclicLogic]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _InitComplete THEN
	_InitComplete := Initialize();
	RETURN;
END_IF

SUPER^.CyclicLogic();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialize" Id="{6642d997-98c2-45af-8731-ca9c93da577c}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD PROTECTED Initialize : BOOL;
VAR
	i : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Initialize := FALSE;
CASE DescendantSequenceState OF
	0: // Register Components
		RegisterComponent(Axis);

		// Set Config
		_LogModeChanges			:= FALSE;
		_LogStateChanges		:= FALSE;
		DescendantSequenceState := DescendantSequenceState + 10;

	10:
		// Copy Mode names
		FOR i := 1 TO 10 DO
			ModeNames[i] := F_UnitModeToString(UDINT_TO_DINT(i));
		END_FOR

		DescendantSequenceState := DescendantSequenceState + 10;

	20:
		IF SUPER^.Initialize() THEN
			DescendantSequenceState := DescendantSequenceState + 10;
		END_IF

	30:
		Initialize := TRUE;
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Starting" Id="{d700bbd5-4ebd-42aa-9db1-b591835e231d}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD PROTECTED Starting]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE _CommandedMode OF
	ePMLProtUnitMode_Production:
		CASE SequenceState OF
			0:
				NoStateTasksToComplete := FALSE;
				SequenceState		   := SequenceState + 10;

			10: // Enable Axis
				NoStateTasksToComplete := FALSE;
				Axis.Enable();
				IF Axis.Enabled THEN
					StateTasksComplete := TRUE;
				END_IF
		END_CASE
END_CASE

SUPER^.Starting();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Stopping" Id="{1d286d67-4c20-4caf-b756-9a8f72182cf7}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD PROTECTED Stopping]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE _CurrentMode OF
	ePMLUnitMode_Production:
		CASE SequenceState OF
			0:
				NoStateTasksToComplete := FALSE;
				SequenceState		   := SequenceState + 10;

			10: // Stop Axis
				Axis.Override := 100;
				Axis.Stop(); // This will also GearOut
				IF Axis.Stopped THEN
					StateTasksComplete := TRUE;
				END_IF
		END_CASE
END_CASE

SUPER^.Stopping();]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_VM">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_VM.Aborting">
      <LineId Id="361" Count="19" />
      <LineId Id="311" Count="0" />
    </LineIds>
    <LineIds Name="FB_VM.AxisRef.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_VM.Completing">
      <LineId Id="210" Count="14" />
      <LineId Id="25" Count="0" />
    </LineIds>
    <LineIds Name="FB_VM.CyclicLogic">
      <LineId Id="25" Count="4" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_VM.Initialize">
      <LineId Id="171" Count="24" />
      <LineId Id="11" Count="0" />
    </LineIds>
    <LineIds Name="FB_VM.Starting">
      <LineId Id="295" Count="15" />
      <LineId Id="137" Count="0" />
    </LineIds>
    <LineIds Name="FB_VM.Stopping">
      <LineId Id="192" Count="15" />
      <LineId Id="151" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>