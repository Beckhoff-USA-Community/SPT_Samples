<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_VM" Id="{e8eb8946-ac6b-4f9a-a349-a2038fff9f99}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_VM EXTENDS FB_PackML_BaseModule
VAR
	Axis : FB_Component_BasicSlaveAxis := (Name := 'Virtual Master');
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="Aborting" Id="{0ab2a755-f479-4155-bea8-7b1b108018b5}">
      <Declaration><![CDATA[METHOD PROTECTED Aborting]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Mode is ignored
CASE SequenceState OF
	0:
		NoStateTasksToComplete := FALSE;
		SUPER^.Aborting();
		SequenceState := SequenceState + 10;

	10: (* Stop Axis *)
		Axis.Override := 100;
		Axis.Stop(); //This will also GearOut
		IF Axis.Stopped THEN
			SequenceState := SequenceState + 10;
		END_IF

	20: (* Disable Axis *)
		Axis.Disable();
		IF NOT Axis.Enabled THEN
			SequenceState := SequenceState + 10;
		END_IF

	30:
		StateTasksComplete := TRUE;
		StateComplete();
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Property Name="AxisRef" Id="{8fbb3c08-11e6-42e9-be14-c11d172359bb}">
      <Declaration><![CDATA[PROPERTY AxisRef : REFERENCE TO AXIS_REF]]></Declaration>
      <Get Name="Get" Id="{2a15330a-5ee0-4def-aebb-a9f06cee8655}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[AxisRef REF= Axis.Axis;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Completing" Id="{3ff898da-49a5-42a9-84dc-dd8eaed69c93}">
      <Declaration><![CDATA[METHOD PROTECTED Completing
VAR
	i				: UDINT;
	SubModulesReady : BOOL := TRUE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE _CurrentMode OF
	ePMLUnitMode_Production:
		CASE SequenceState OF
			0:
				NoStateTasksToComplete := FALSE;
				Axis.Stop();
				IF Axis.Stopped THEN
					SequenceState := SequenceState + 10;
				END_IF

			10:
				SequenceState := SequenceState + 10;

			20:
				StateTasksComplete := TRUE;
		END_CASE

	ePMLProtUnitMode_Maintenance, ePMLProtUnitMode_Manual:
		;
END_CASE

SUPER^.Completing();]]></ST>
      </Implementation>
    </Method>
    <Method Name="CyclicLogic" Id="{099d02ae-52c4-4630-a76e-d553625d2bca}">
      <Declaration><![CDATA[METHOD PUBLIC CyclicLogic
VAR
	i : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT InitComplete THEN
	_InitComplete := Initialize();
	RETURN;
END_IF

SUPER^.CyclicLogic();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Initialize" Id="{6642d997-98c2-45af-8731-ca9c93da577c}">
      <Declaration><![CDATA[METHOD PROTECTED Initialize : BOOL;
VAR
	i						 : UDINT;
	varInfo					 : __SYSTEM.VAR_INFO;
	AllSubUnitsInitialized	 : BOOL := TRUE;
	AllComponentsInitialized : BOOL := TRUE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Initialize := FALSE;
CASE DescendantSequenceState OF
	0:
		RegisterComponent(Axis);
		_LogModeChanges			:= FALSE;
		_LogStateChanges		:= FALSE;
		DescendantSequenceState := DescendantSequenceState + 10;

	10:
		//Copy Mode names
		FOR i := 1 TO 10 DO
			ModeNames[i] := F_UnitModeToString(UDINT_TO_DINT(i));
		END_FOR

		DescendantSequenceState := DescendantSequenceState + 10;

	20:
		IF SUPER^.Initialize() THEN
			DescendantSequenceState := DescendantSequenceState + 10;
		END_IF

	30:
		DescendantSequenceState := DescendantSequenceState + 10;

	40:
		Initialize := TRUE;
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Starting" Id="{d700bbd5-4ebd-42aa-9db1-b591835e231d}">
      <Declaration><![CDATA[METHOD PROTECTED Starting
VAR
	i				: UDINT;
	SubModulesReady : BOOL := TRUE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE _CommandedMode OF
	ePMLProtUnitMode_Production:
		CASE SequenceState OF
			0:
				// Delay 1 scan to make sure timer is off.  Not guarnteed from last time where it finished.
				NoStateTasksToComplete := FALSE;
				SequenceState		   := SequenceState + 10;

			10:
				NoStateTasksToComplete := FALSE;
				Axis.Enable();
				IF Axis.Enabled THEN
					SequenceState := SequenceState + 10;
				END_IF

			20:
				SequenceState := SequenceState + 10;

			30:
				StateTasksComplete := TRUE;
		END_CASE
END_CASE

SUPER^.Starting();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Stopping" Id="{1d286d67-4c20-4caf-b756-9a8f72182cf7}">
      <Declaration><![CDATA[METHOD PROTECTED Stopping
VAR
	i				: UDINT;
	SubModulesReady : BOOL := TRUE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE _CurrentMode OF
	ePMLUnitMode_Production:
		CASE SequenceState OF
			0:
				NoStateTasksToComplete := FALSE;
				Axis.Stop(); //This will also GearOut
				IF Axis.Stopped THEN
					SequenceState := SequenceState + 10;
				END_IF

			10:
				StateTasksComplete := TRUE;
		END_CASE
END_CASE

SUPER^.Stopping();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>