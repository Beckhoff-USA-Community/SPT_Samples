<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="BasicESPG_Velocity" Id="{b4475371-13b5-456f-932b-3b84efa817dd}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM BasicESPG_Velocity
VAR
	Basic_Axis : AXIS_REF;
	Power	   : MC_Power;
	EnableSPG  : MC_ExtSetPointGenEnable;
	DisableSPG : MC_ExtSetPointGenDisable;

	//
	Execute					: BOOL;
	NextIncrementalVelocity : LREAL;
	
	Inc1: BOOL;
	Inc10: BOOL;
	Dec1: BOOL;
	Dec10: BOOL;
	HasJob: BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// The Axis is always enabled for demo purposes
Power(Axis			  := Basic_Axis,
	  Enable		  := TRUE,
	  Enable_Positive := TRUE,
	  Enable_Negative := TRUE);

// Enable the External Setpoint Generator
EnableSPG(Axis := Basic_Axis, Execute := Execute, Position := 0);

// Disable the External Setpoint Generator when not enabled
DisableSPG(Axis := Basic_Axis, Execute := NOT Execute);

(*
	Once enabled above, the Velocity value can be modified to send commands to the axis through the Axis.PlcToNc.ExtSetVelo
	Position is not required as the MC_ExtSetPointGenFeed is sending Velocity commands to the axis 
	- Actualy velocity for a real axis will be based on encoder feedback
	- For a simulation axis, actual velocity will be 0 and the Actual Position will not change
	An Acceleration value is not required for Velocity commands
	If the Direction is 0 the axis will not move, set the value to 1 for movemnt. The actual direction will be determined by the sign of the Velocity
*)

// When the Axis Status 'Has Job' is true, no other commands can be sent to the Axis. While the ESPG is Enabled, the 'Has Job' will be TRUE 
HasJob := Basic_Axis.NcToPlc.StateDWord.8;

//
IF EnableSPG.Enabled THEN // Not required, but prevents the NextIncrementalVelocity value from being sent to Axis.PlcToNc.ExtSetVelo
	MC_ExtSetPointGenFeed(Axis		   := Basic_Axis,
						  Position	   := 0,
						  Velocity	   := NextIncrementalVelocity,
						  Acceleration := 0,
						  Direction	   := 1);
END_IF

(*	WARNING !!!!!!!!
	Changing the Velocity while the ESPG is not Enabled will change the value of the Axis.PlcToNc.ExtSetVelo, but will not cause Axis movement.
*)

// Commands from Visu
IF Inc1 THEN
	Inc1					:= FALSE;
	NextIncrementalVelocity := NextIncrementalVelocity + 1;
END_IF

IF Inc10 THEN
	Inc10					:= FALSE;
	NextIncrementalVelocity := NextIncrementalVelocity + 10;
END_IF

IF Dec1 THEN
	Dec1					:= FALSE;
	NextIncrementalVelocity := NextIncrementalVelocity - 1;
END_IF

IF Dec10 THEN
	Dec10					:= FALSE;
	NextIncrementalVelocity := NextIncrementalVelocity - 10;
END_IF]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>